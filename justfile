set export

_default:
    just --list

build:
    docker compose build

up:
    docker compose up

down:
    docker compose down --remove-orphans

# bg stands for background
up-bg:
    docker compose up -d

makemigrations:
    docker compose run --rm api python manage.py makemigrations

postgres-version:
    docker compose exec -T postgres bash -c 'psql -U "$POSTGRES_USER" -d "$POSTGRES_DB" -c "SHOW server_version;"'

postgres-dump:
    #!/usr/bin/env bash
    set -euo pipefail
    out_dir="dumps"
    db_name="$(docker compose exec -T postgres bash -c 'printf %s "${POSTGRES_DB:-}"')"
    if [ -z "$db_name" ]; then
        db_name="$(docker compose exec -T postgres bash -c 'psql -U "${POSTGRES_USER:-postgres}" -d postgres -Atqc "SELECT datname FROM pg_database WHERE datistemplate = false ORDER BY datname LIMIT 1;"')"
    fi
    db_name="${db_name:-postgres}"
    ts="$(date +%Y%m%d-%H%M%S)"
    out_file="$out_dir/$db_name-$ts.sql"
    mkdir -p "$out_dir"
    docker compose exec -T postgres bash -c "pg_dump -U \"${POSTGRES_USER:-postgres}\" -d \"$db_name\"" > "$out_file"
    test -s "$out_file"
    echo "Created PostgreSQL dump: $out_file"

postgres-restore DUMP_FILE FORCE='':
    #!/usr/bin/env bash
    set -euo pipefail
    dump_file="{{DUMP_FILE}}"
    if [ ! -r "$dump_file" ]; then
        echo "Dump file not found or not readable: $dump_file" >&2
        exit 1
    fi
    db_name="$(docker compose exec -T postgres bash -c 'printf %s "${POSTGRES_DB:-}"')"
    if [ -z "$db_name" ]; then
        db_name="$(docker compose exec -T postgres bash -c 'psql -U "${POSTGRES_USER:-postgres}" -d postgres -Atqc "SELECT datname FROM pg_database WHERE datistemplate = false ORDER BY datname LIMIT 1;"')"
    fi
    db_name="${db_name:-postgres}"
    force_flag="{{FORCE}}"
    if [ "$force_flag" != "--force" ]; then
        echo "Refusing to overwrite existing schema without --force." >&2
        echo "Usage: just postgres-restore <dump.sql> --force" >&2
        exit 2
    fi
    echo "Restoring PostgreSQL dump (forced): $dump_file -> $db_name"
    # Plain SQL dumps are not idempotent: wipe public schema before restore.
    docker compose exec -T postgres bash -c "psql -v ON_ERROR_STOP=1 -U \"${POSTGRES_USER:-postgres}\" -d \"$db_name\" -c 'DROP SCHEMA IF EXISTS public CASCADE; CREATE SCHEMA public;'"
    docker compose exec -T postgres bash -c "psql -v ON_ERROR_STOP=1 -U \"${POSTGRES_USER:-postgres}\" -d \"$db_name\"" < "$dump_file"
    echo "Restore complete: $dump_file"

postgres-upgrade-17-to-18:
    ./scripts/upgrade-postgres-17-to-18-local.sh

# Resync every postgres sequence in the public schema to MAX(id) of its owning
# column. Idempotent. Fixes UniqueViolation in Django's post-migrate signal
# when a sequence drifts below MAX(id) after a pg_dump restore.
sync-sequences:
    #!/usr/bin/env bash
    set -euo pipefail
    docker compose exec -T postgres bash -c 'psql -U "$POSTGRES_USER" -d "$POSTGRES_DB" -v ON_ERROR_STOP=1 -q' <<'SQL'
    DO $$
    DECLARE r record; mv bigint;
    BEGIN
      FOR r IN
        SELECT n.nspname AS schema, c.relname AS seq, t.relname AS tbl, a.attname AS col
        FROM pg_class c
        JOIN pg_depend d ON d.objid = c.oid AND d.deptype = 'a'
        JOIN pg_class t ON d.refobjid = t.oid
        JOIN pg_attribute a ON a.attrelid = t.oid AND a.attnum = d.refobjsubid
        JOIN pg_namespace n ON n.oid = c.relnamespace
        WHERE c.relkind = 'S' AND n.nspname = 'public'
      LOOP
        EXECUTE format('SELECT COALESCE(MAX(%I), 0) FROM %I.%I', r.col, r.schema, r.tbl) INTO mv;
        EXECUTE format('SELECT setval(%L, %s)', r.schema || '.' || r.seq, GREATEST(mv, 1));
      END LOOP;
    END $$;
    SQL

migrate: sync-sequences
    docker compose run --rm api python manage.py migrate

restart-api:
    docker compose restart api

pytest:
    API_ENV_FILE=config/test.env docker compose run --rm api python -m pytest

pytest-focused:
    mkdir -p .test-results && chmod 777 .test-results
    API_ENV_FILE=config/test.env docker compose run --rm -e USE_SQLITE_FOR_TESTS=1 api python -m pytest apps/annotations/tests/tests.py apps/search/tests/test_services.py -q --junitxml=/app/.test-results/junit-focused.xml

pytest-search:
    API_ENV_FILE=config/test.env docker compose run --rm api python -m pytest apps/search/tests/ -v

coverage:
    mkdir -p .test-results && chmod 777 .test-results
    API_ENV_FILE=config/test.env docker compose run --rm -e COVERAGE_FILE=/tmp/.coverage api python -m pytest --cov=apps --cov=config --cov-report=term-missing --cov-report=xml:/app/.test-results/coverage.xml --cov-fail-under=55 --junitxml=/app/.test-results/junit.xml

shell:
    docker compose run --rm api python manage.py shell_plus

bash:
    docker compose run --rm api bash

# Meilisearch: create indexes and sync from DB (run after first deploy or when index_not_found)
setup-search-indexes:
    docker compose run --rm api python manage.py setup_search_indexes

sync-search-index INDEX:
    docker compose run --rm api python manage.py sync_search_index {{INDEX}}

sync-all-search-indexes:
    docker compose run --rm api python manage.py sync_all_search_indexes

clean:
    uvx ruff check --fix .

check-architecture:
    uv run python scripts/check_architecture_boundaries.py

celery_status:
    docker compose run --rm api celery -A config inspect active
